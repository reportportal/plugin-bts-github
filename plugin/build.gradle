import com.github.spotbugs.SpotBugsTask

plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.github.spotbugs" version "3.0.0"
    id "com.github.node-gradle.node" version "3.5.1"
}

apply from: 'project-properties.gradle'
apply from: 'ui.gradle'

repositories {
    mavenCentral()
    mavenLocal()
    if (releaseMode) {
        dependencyRepos.forEach { path ->
            maven {
                setUrl("https://maven.pkg.github.com/reportportal/${path}")
                credentials {
                    username = findProperty("githubUserName")
                    password = findProperty("githubToken")
                }
            }
        }
    } else {
        maven { url 'https://jitpack.io' }
    }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version')
                : "com.github.reportportal:commons-bom:$reportportalCommonsBomVersion")
        mavenBom "org.junit:junit-bom:$junitBomVersion"
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
    } else {
        implementation "com.github.reportportal:plugin-api:$pluginApiVersion"
        annotationProcessor "com.github.reportportal:plugin-api:$pluginApiVersion"
    }
    implementation "org.apache.commons:commons-lang3:$apacheCommonsLangVersion"
    implementation "org.apache.commons:commons-collections4:$apacheCommonsCollectionsVersion"

    implementation "org.jasypt:jasypt:$jasyptVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.mockito:mockito-junit-jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
}

spotbugs {
    sourceSets = [sourceSets.main]
    reportLevel = "high"
}
tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

test {
    useJUnitPlatform()
    maxParallelForks = 1
    testLogging {
        events = ['failed']
        exceptionFormat = 'short'
    }
    reports {
        junitXml.enabled = true
    }
}

build.dependsOn jacocoTestReport

artifacts {
    archives shadowJar
}

sourceSets {
    main {
        resources
                {
                    exclude '**'
                }
    }
}

jar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginId}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Report Portal",
                "Plugin-Class": "com.epam.reportportal.extension.github.GitHubPlugin",
                "Plugin-Service": "api"
        )
    }
}

shadowJar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    configurations = [project.configurations.compile]
    zip64 true
    dependencies {
        exclude(dependency('com.github.reportportal:'))
        exclude(dependency('com.epam.reportportal:'))
        exclude(dependency('org.springframework:'))
        exclude(dependency('org.springframework.security:'))
        exclude(dependency('org.springframework.data:'))
        exclude(dependency('org.springframework.boot:'))
        exclude(dependency('ch.qos.logback:'))
        exclude(dependency('org.slf4j:'))
    }
}

task plugin(type: Jar) {
    getArchiveBaseName().set("plugin-${pluginId}")
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

compileJava.dependsOn npm_run_build

